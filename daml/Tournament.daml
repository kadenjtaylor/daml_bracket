module Tournament where

import DA.Map (null, fromList, delete, empty, insert, values, Map, lookup, toList)
import DA.Foldable (foldMap, Foldable(maximum))
import DA.Optional (catOptionals, isNone, mapOptional)

type MatchIndex = Int

data MatchRef
  = PartyRef with
    p: Party
  | IndexRef with
    index: MatchIndex
  deriving (Eq, Show, Ord)

data Match
  = Bye with
    p: MatchRef
  | Vs with
    p1: MatchRef
    p2: MatchRef
      deriving (Eq, Show)

data PartialBracket = PartialBracket with
  round: [MatchIndex]
  matches: Map MatchIndex Match
    deriving (Eq, Show)

data Bracket
  = Empty
  | NonEmpty with
      root: MatchIndex
      matches: Map MatchIndex Match
    deriving (Eq, Show)

data MatchReport = MatchReport with
  index: MatchIndex
  winner: Party
    deriving (Eq, Show)

data BracketUpdateError
  = BracketEmpty
  | PlayerNotActive with
    id: MatchIndex
    p: Party
  | NoSuchMatch with
    id: MatchIndex
  | MatchNotReady with
    matchId: MatchIndex
    blocking: [MatchIndex]
  | UnknownError with
    msg: Text
      deriving (Eq, Show)

data ReportResult
  = Finished with
    winner: Party
  | Next with
    b: Bracket
  | Error with
    err: BracketUpdateError

data ActiveMatch
  = ActiveBye with
    index: MatchIndex
    p: Party
  | ActiveVs with
    index: MatchIndex
    p1: Party
    p2: Party
      deriving (Eq, Show)

-- ========================================================

bracketize: [Party] -> Bracket
bracketize [] = Empty
bracketize ps = finalize $ toPartial $ matchUp ps

matchUp: [Party] -> [Match]
matchUp [] = []
matchUp [p] = [Bye (PartyRef p)]
matchUp (p1 :: p2 :: rest) = Vs (PartyRef p1) (PartyRef p2) :: matchUp rest

nextIndex: [MatchIndex] -> MatchIndex
nextIndex [] = 0
nextIndex indicies = maximum indicies + 1

-- TODO: Investigate different bracketing strategies
finalize: PartialBracket -> Bracket
finalize (PartialBracket [] m) = Empty
finalize (PartialBracket [i] m) = NonEmpty with
  root = i
  matches = m
finalize (PartialBracket (i1 :: i2 :: rest) matches) =
  do
  let i = nextIndex (i1 :: i2 :: rest)
      nextMatch = Vs (IndexRef i1) (IndexRef i2)
  finalize PartialBracket with
    round = rest ++ [i]
    matches = insert i nextMatch matches

toPartial: [Match] -> PartialBracket
toPartial ms = foldl load emptyPartial ms

-- TODO: Refactor Partial Bracket -- looks Foldable to me
emptyPartial : PartialBracket
emptyPartial = PartialBracket with
  round = []
  matches = empty

load: PartialBracket -> Match -> PartialBracket
load (PartialBracket r m) toLoad =
  do
  let i = nextIndex r
  PartialBracket with
    round = r ++ [i]
    matches = insert i toLoad m

toActive: (MatchIndex, Match) -> Optional ActiveMatch
toActive (index, m) = case m of
  Vs (PartyRef p1) (PartyRef p2) -> Some $ ActiveVs index p1 p2
  Bye(PartyRef p) -> Some $ ActiveBye index p
  _ -> None

activeMatches: Bracket -> [ActiveMatch]
activeMatches b = mapOptional toActive (toList b.matches)

indexRefsOf: Match -> [MatchIndex]
indexRefsOf (Bye (IndexRef i)) = [i]
indexRefsOf (Vs (IndexRef i1) (IndexRef i2)) = [i1, i2]
indexRefsOf (Vs (IndexRef i1) _) = [i1]
indexRefsOf (Vs _ (IndexRef i2)) = [i2]
indexRefsOf _ = []

partyRefsOf: Match -> [Party]
partyRefsOf (Bye (PartyRef p)) = [p]
partyRefsOf (Vs (PartyRef p1) (PartyRef p2)) = [p1, p2]
partyRefsOf (Vs (PartyRef p1) _) = [p1]
partyRefsOf (Vs _ (PartyRef p2)) = [p2]
partyRefsOf _ = []

isWellFormed: Bracket -> Bool
isWellFormed Empty = True
isWellFormed (NonEmpty root matches) = formCheck ([root], matches)

formCheck: ([MatchIndex], Map MatchIndex Match) -> Bool
formCheck ([], m) | DA.Map.null m = True
formCheck ([], _) = False
formCheck (indicies, m) = do
  let stuff = map (`DA.Map.lookup` m) indicies
  not (any isNone stuff) &&
    (do let nextRefs = foldMap indexRefsOf (catOptionals stuff)
        let nextMap = foldl deleteStep m indicies
        formCheck (nextRefs, nextMap))
      
deleteStep: Ord k => Map k v -> k -> Map k v
deleteStep m k = delete k m

playersIn: Bracket -> [Party]
playersIn Empty = []
playersIn (NonEmpty root matches) = foldMap partyRefsOf (values matches)

alterIfDependentOn: MatchReport -> (MatchIndex, Match) -> (MatchIndex, Match)
alterIfDependentOn (MatchReport reportIndex winner) (matchindex, (Vs (IndexRef i) ref))
  | reportIndex == i = (matchindex, Vs (PartyRef winner) ref)
alterIfDependentOn (MatchReport reportIndex winner) (matchindex, (Vs ref (IndexRef i)))
  | reportIndex == i = (matchindex, Vs ref (PartyRef winner))
alterIfDependentOn (MatchReport reportIndex winner) (matchindex, (Bye (IndexRef i)))
  | reportIndex == i = (matchindex, Bye (PartyRef winner))
alterIfDependentOn _ (matchindex, m) = (matchindex, m)

updateMatches: Map MatchIndex Match -> MatchReport -> Map MatchIndex Match
updateMatches matches report = do
    let m = delete report.index matches
        updated = map (alterIfDependentOn report) (toList m)
    fromList updated

-- TODO: Clean up similar logic in main cases
applyReport: Bracket -> MatchReport -> ReportResult
applyReport Empty _ = Error BracketEmpty
applyReport (NonEmpty root matches) report@(MatchReport index winner) = do
  let currentMatch = DA.Map.lookup index matches
  case (currentMatch) of
    Some(Bye(PartyRef p)) ->
      if p == winner then
        if root == index then
          Finished winner
        else
          Next $ (NonEmpty root (updateMatches matches report))
      else
        Error $ PlayerNotActive index winner
    Some(Vs (PartyRef p1) (PartyRef p2)) ->
      if p1 == winner || p2 == winner then
        if root == index then
          Finished winner
        else
          Next $ (NonEmpty root (updateMatches matches report))
      else
        Error $ PlayerNotActive index winner
    Some(Bye(IndexRef i)) -> Error $ MatchNotReady index [i]
    Some(Vs (IndexRef i1) (IndexRef i2)) -> Error $ MatchNotReady index [i1, i2]
    Some(Vs (_) (IndexRef i)) -> Error $ MatchNotReady index [i]
    Some(Vs (IndexRef i) (_)) -> Error $ MatchNotReady index [i]
    None -> Error $ NoSuchMatch index